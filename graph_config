# single line comment begins with '#'

# For viewing - mouse and keys "w" - forward, "s" - back, "d" - right, "a" - left.
# View the next graph - "Space" key,
# re-parsing of the file "plot_config" -key "e", exit- "q";
# Parsing errors are reported in "parse_report".


# optional constants {...} block, with global constant declarations,
# the reserved words "time", "s", "t" should not be used,
# as well as the names of built-in functions:
# "abs", "sqrt", "exp", "log", "cos", "sin", "tan", "acos", "asin", "atan".

constants
{
    pi=3.14159;
    # torus
    rTor=0.5;
    RTor=1.5;
} 

# optional functions {...} block, with definitions of user functions,
# see the naming convention above; functions can use names
# other functions defined earlier.
functions
{
    sq(x)=x*x;
    gauss(x,y,a,sigma,x0,y0)=a*exp(-sigma*(sq(x-x0)+sq(y-y0)));
}               

# Followed by one or more plot blocks {....}
# with graph parameters
                    
#plane wave
plot
{    
    # one or more surface display flags,
    # listed with ";"
    # allowed flags:
    colored;  # -colored surface,
    mesh;     #  -grid,
    zlevels;  # -line level along the z-axis, "ylevels" and "xlevels" are also allowed,
    box;      # -bounding box,
    specular; # -reflective surface,
    points
    {            
         # animation type: "limit" - animation for the specified time
         # "period" - cyclic repeat with the specified period,
         # if there is no type, the graph is static.
         limit = 60.0;
 
         # "s", "t" -parameters
         s(-3,3,40); # both parameters range from -3 to +3 in steps (3 - (- 3)) / 40
         t(-3,3,40);
         # x, y, z -parametric equations of the surface,
         # as functions "s", "t" and "time"
         x = s;
         y = t;
         z = sin (s-time) + cos (t + time);
    }
}

#cylindrical wave
plot
{
    specular; #reflective surface
    points
    {
        limit = 60.0;
        s (0,5,80); # distance from axis
        t (-3.141,3.141,40); # angle in polar coordinates
        x = s * cos (t);
        y = s * sin (t);
        z = 1 / (0.3 + s) * cos (4.0 * time-4.0 * s);
    }
}

# revolving torus
plot
{
    # colored and reflective surface
    specular;
    colored;
    points
    {
         limit = 60.0;
         s (-3.141,3.141,40);
         t (-3.141,3.141,40);
         x = cos (s) * (RTor + rTor * cos (t)) * cos (time) + rTor * sin (t) * sin (time);
         y = sin (s) * (RTor + rTor * cos (t));
         z = rTor * sin (t) * cos (time) -cos (s) * (RTor + rTor * cos (t)) * sin (time);
    }
}

#static surface
plot
{
    specular;
    points
    {            
         s(-5,5,80);    
         t(-5,5,80);    
         x=s;
         y=t;
         z=gauss(s,t,2.0,1.0,0.0,0.0)+
           gauss(s,t,1.0,0.5,2.0,2.0)+
           gauss(s,t,4.0,1.5,-3.0,-3.0);
    }
}

